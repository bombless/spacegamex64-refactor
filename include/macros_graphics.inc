; MACROS_GRAPHICS.INC - Graphics rendering macros

IFNDEF MACROS_GRAPHICS_INC
MACROS_GRAPHICS_INC EQU 1

BLTRECT MACRO sourceX, sourceY, destX, destY, W, H
    MOV RCX, GOP
    MOV R8, 00H
    MOV R9, sourceX
    PUSH 0
    PUSH H
    PUSH W
    PUSH destY
    PUSH destX
    PUSH sourceY
    SUB RSP, 20h
    CALL BLT
    ADD RSP, 20h
    ADD RSP, 30h
ENDM

BLTBUFF MACRO sourceX, sourceY, destX, destY, W, H
    MOV RCX, GOP
    MOV R8, 02H
    MOV R9, sourceX
    PUSH 0
    PUSH H
    PUSH W
    PUSH destY
    PUSH destX
    PUSH sourceY
    SUB RSP, 20h
    CALL BLT
    ADD RSP, 20h
    ADD RSP, 30h
ENDM

RENDERTILE MACRO TRANSPARENCY
    LOCAL RENDER8x8LOOP
    LOCAL PUSHPIXELTOTILE
    LOCAL PUSHPIXELNOOFFSET
    LOCAL AFTERPUSHPTT
    LOCAL AFTERRENDER8x8
    
    MOV R9, RAX
    
    MOV RAX, RBX
    XOR RDX, RDX
    MOV R10, 020H
    DIV R10
    MOV RBX, RDX
    MOV R10, 02000H
    MUL R10
    SHL RBX, 05
    ADD RAX, RBX
    LEA R12, QWORD PTR [OUTPUTFBUFFER]

    SHL R9, 08H
    LEA R10, QWORD PTR [bgTiles]
    ADD R9, R10
    MOV RCX, 00H
    MOV R11, RAX
    MOV RAX, (TRANSPARENCY)
RENDER8x8LOOP:
    MOV EBX, DWORD PTR [R9]
        CMP AL, 00H
        JZ PUSHPIXELTOTILE
        TEST EBX, 0FFFFFFH
        JZ AFTERPUSHPTT
PUSHPIXELTOTILE:
        CMP AL, 00H
        JNZ PUSHPIXELNOOFFSET
        PUSH RAX
        XOR RAX, RAX
        MOV R14, R11
        SHR R14, 02H
        MOV AL, BYTE PTR [TM0XOFFSET]
        ADD RAX, R14
        AND RAX, 0FFH
        SHL RAX, 02H
        MOV R13, RAX
        XOR RAX, RAX
        MOV R14, R11
        SHR R14, 0AH
        MOV AL, BYTE PTR [TM0YOFFSET]
        ADD RAX, R14
        AND RAX, 0FFH
        SHL RAX, 0AH
        ADD R13, RAX
        POP RAX
    MOV DWORD PTR [R12 + R13], EBX
    JMP AFTERPUSHPTT
PUSHPIXELNOOFFSET:
    MOV DWORD PTR [R12 + R11], EBX
AFTERPUSHPTT:
    ADD R11, 04H
    ADD R9, 04H
    INC RCX
    TEST RCX, 07H
    JNZ RENDER8x8LOOP
        CMP RCX, 040H
        JZ AFTERRENDER8x8
        ADD R11, 03E0H
        JMP RENDER8x8LOOP
AFTERRENDER8x8:
ENDM

RENDERSPRITE MACRO
    LOCAL AFTERPLOTSPRITEPIXEL
    LOCAL POSTTILESSELECTED
    LOCAL SPRITEPIXELLOOP
    LOCAL NOTNEXTPIXELYET2
    
    SHR RAX, 10H
    XOR RBX, RBX
    MOV BX, AX
    SHL RBX, 0CH
    
    LEA RDX, QWORD PTR [spriteTiles]
    CMP SPRITEVERSION, 00H
    JZ POSTTILESSELECTED
        LEA RDX, QWORD PTR [spriteTiles2]
POSTTILESSELECTED:
    ADD RBX, RDX
    LEA RDX, QWORD PTR [OUTPUTFBUFFER]
    SHR RAX, 10H
    XOR R8, R8
    XOR R9, R9
    MOV R8, RAX
    AND R8, 0FFH
    SHL R8, 02H
    MOV R9, RAX
    SHR R9, 06H
    AND R9, 03FC00H
    ADD RDX, R8
    ADD RDX, R9
    MOV RCX, 20H
    XOR R8, R8
SPRITEPIXELLOOP:
    MOV EAX, DWORD PTR [RBX]
        CMP EAX, 00H
        JZ AFTERPLOTSPRITEPIXEL
    MOV DWORD PTR [RDX], EAX
AFTERPLOTSPRITEPIXEL:
    ADD RBX, 04H
    ADD RDX, 04H
    INC R8
    TEST R8, 01FH
    JNZ SPRITEPIXELLOOP
        ADD RDX, 0380H
        XOR R8, R8
    LOOP SPRITEPIXELLOOP
ENDM

RENDERGRAPHICS MACRO
    LOCAL ENEMIESTOSPRITES
    LOCAL NEXTENEMYSPRITE
    LOCAL BOLTTOSPRITES
    LOCAL NEXTBOLTSPRITE
    LOCAL RENDERTILEMAP0
    LOCAL RENDERTILEMAP1
    LOCAL AFTERTM1TILE
    LOCAL RESETUPSCALEVALLOOP
    LOCAL SENDTOSCREEN
    LOCAL AMPLIFYLOOP
    LOCAL AFTERSENDTOSCREEN
    LOCAL HARDWAREACCEL
    LOCAL RENDERSPRITELOOP
    LOCAL SPRITELOOPTAIL
    LOCAL SKIPRESET
    
    ; RENDER BOLTS TO SPRITES
    LEA RBX, QWORD PTR [BOLTS]
    LEA RDX, QWORD PTR [SPRITES]
    ADD RDX, 080H
    MOV RCX, 08H
BOLTTOSPRITES:
    XOR RAX, RAX
    MOV [RDX], RAX
    MOV AL, BYTE PTR [RBX]
    CMP AL, 00H
    JZ NEXTBOLTSPRITE
        MOV RAX, [RBX]
        MOV [RDX], RAX
NEXTBOLTSPRITE:
    ADD RBX, BOLTOBJSIZE
    ADD RDX, 08H
    LOOP BOLTTOSPRITES
    
    ; RENDER ENEMIES TO SPRITES
    LEA RBX, QWORD PTR [ENEMY]
    MOV RCX, NUMBEROFENEMY
ENEMIESTOSPRITES:
    XOR RAX, RAX
    MOV [RDX], RAX
    MOV AL, BYTE PTR [RBX]
    CMP AL, 00H
    JZ NEXTENEMYSPRITE
    TEST AL, 04H
    JNZ NEXTENEMYSPRITE
        MOV RAX, [RBX]
        MOV [RDX], RAX
NEXTENEMYSPRITE:
    ADD RBX, ENEMYOBJSIZE
    ADD RDX, 08H
    LOOP ENEMIESTOSPRITES
    
    ; RENDER TILE MAP 0
    XOR RBX, RBX
    LEA RDX, TILEMAP0
RENDERTILEMAP0:
    XOR RAX, RAX
    MOV AX, WORD PTR [RDX + RBX]
        PUSH RBX
        PUSH RDX
        SHR RBX, 01H
    RENDERTILE 00H
        POP RDX
        POP RBX
    INC RBX
    INC RBX
    CMP RBX, 0800H
    JNZ RENDERTILEMAP0
    
    ; RENDER SPRITES BACK TO FRONT
    LEA RBX, QWORD PTR [SPRITES]
    ADD RBX, 03F8H
    MOV RCX, 00H
RENDERSPRITELOOP:
    MOV RAX, QWORD PTR [RBX]
    CMP AL, 00H
    JZ SPRITELOOPTAIL
        PUSH RBX
        PUSH RCX
        RENDERSPRITE
        POP RCX
        POP RBX
SPRITELOOPTAIL:
    SUB RBX, 08H
    INC RCX
    CMP RCX, 080H
    JNZ RENDERSPRITELOOP

    ; RENDER TILE MAP 1
    XOR RBX, RBX
    LEA R8, TILEMAP1
RENDERTILEMAP1:
    XOR RAX, RAX
    MOV AX, WORD PTR [R8 + RBX]
    CMP AX, 00H
    JZ AFTERTM1TILE
        PUSH RBX
        PUSH R8
        SHR RBX, 01H
    RENDERTILE 01H
        POP R8
        POP RBX
AFTERTM1TILE:
    INC RBX
    INC RBX
    CMP RBX, 0800H
    JNZ RENDERTILEMAP1

    ; OUTPUT GRAPHICS TO SCREEN
    MOV RAX, UPSCALE_MODE
    CMP RAX, 00H
    JZ HARDWAREACCEL
        LEA RDX, QWORD PTR [OUTPUTFBUFFER]
        BLTBUFF 0, 0, 512, 384, 256, 256
    JMP AFTERSENDTOSCREEN

HARDWAREACCEL:
    MOV RCX, 020H
    MOV RAX, 00H
    LEA RBX, QWORD PTR [UPSCALEDROW]
RESETUPSCALEVALLOOP:
    MOV QWORD PTR [RBX], RAX
    ADD RBX, 08H
    LOOP RESETUPSCALEVALLOOP
    MOV QWORD PTR [UPSCALEDCOUNT], RAX

    MOV RCX, MPServices
    LEA RDX, [PARALLELRENDERTOSCREEN]
    MOV R8, 00H
    MOV R9, 00H
    SUB RSP, 8
    PUSH QWORD PTR [FAILEDCPULIST]
    PUSH 00H
    PUSH 00H
    CALL StartupAllAPs
    
    MOV RAX, QWORD PTR [UPSCALEDCOUNT]
    OUTPUT64BITNUMBER 00H
    
    MOV RAX, QWORD PTR [UPSCALEDCOUNT]
    CMP RAX, 00H
    JNZ SKIPRESET
        INC UPSCALE_MODE
        RESETTITLE
SKIPRESET:
    
    MOV RDX, UPSCALEDBUFFER
    BLTBUFF 0, 0, 128, 0, 1024, 1024
    
AFTERSENDTOSCREEN:
ENDM

RENDERHITBOXES MACRO
    LOCAL ENEMYRENDERHBLOOP
    LOCAL NEXTEHB
    LOCAL BOLTRENDERHBLOOP
    LOCAL NEXTBHB
    
    ; SHOW SHIP HITBOX
    XOR RAX, RAX
    MOV AL, BYTE PTR [SHIPHITBOX]
    MOV R10, RAX
        SHL R10, 02H
        ADD R10, 080H
    MOV AL, BYTE PTR [SHIPHITBOX + 01H]
    MOV R11, RAX
        SHL R11, 02H
    MOV AL, BYTE PTR [SHIPHITBOX + 02H]
    MOV R12, RAX
        SHL R12, 02H
    MOV AL, BYTE PTR [SHIPHITBOX + 03H]
    MOV R13, RAX
        SHL R13, 02H
    
    LEA RDX, QWORD PTR [COLORRED]
    XOR RAX, RAX
    MOV AL, BYTE PTR [SHIPALT]
    SHL RAX, 02H
    ADD RDX, RAX
    BLTRECT 0, 0, R10, R11, R12, R13
    
    ; ENEMY HITBOXES
    MOV RCX, NUMBEROFENEMY
    LEA RBX, QWORD PTR [ENEMY]
ENEMYRENDERHBLOOP:
    MOV AL, BYTE PTR [RBX]
    CMP AL, 00H
    JZ NEXTEHB
    
    XOR RAX, RAX
    MOV AL, BYTE PTR [RBX + 04H]
    ADD AL, BYTE PTR [RBX + 08H]
    MOV R10, RAX
        SHL R10, 02H
        ADD R10, 080H
    MOV AL, BYTE PTR [RBX + 06H]
    ADD AL, BYTE PTR [RBX + 09H]
    MOV R11, RAX
        SHL R11, 02H
    MOV AL, BYTE PTR [RBX + 0AH]
    MOV R12, RAX
        SHL R12, 02H
    MOV AL, BYTE PTR [RBX + 0BH]
    MOV R13, RAX
        SHL R13, 02H
    
    LEA RDX, QWORD PTR [COLORRED]
    XOR RAX, RAX
    MOV AL, BYTE PTR [RBX + 0CH]
    SHL RAX, 02H
    ADD RDX, RAX
        PUSH RBX
        PUSH RCX
    BLTRECT 0, 0, R10, R11, R12, R13
        POP RCX
        POP RBX
NEXTEHB:
    ADD RBX, ENEMYOBJSIZE
    DEC RCX
    CMP RCX, 00H
    JNZ ENEMYRENDERHBLOOP
    
    ; BOLT HITBOXES
    MOV RCX, 08H
    LEA RBX, QWORD PTR [BOLTS]
BOLTRENDERHBLOOP:
    MOV AL, BYTE PTR [RBX]
    CMP AL, 00H
    JZ NEXTBHB
    
    XOR RAX, RAX
    MOV AL, BYTE PTR [RBX + 04H]
    ADD AL, BYTE PTR [RBX + 08H]
    MOV R10, RAX
        SHL R10, 02H
        ADD R10, 080H
    MOV AL, BYTE PTR [RBX + 06H]
    ADD AL, BYTE PTR [RBX + 09H]
    MOV R11, RAX
        SHL R11, 02H
    MOV AL, BYTE PTR [RBX + 0AH]
    MOV R12, RAX
        SHL R12, 02H
    MOV AL, BYTE PTR [RBX + 0BH]
    MOV R13, RAX
        SHL R13, 02H
    
    LEA RDX, QWORD PTR [COLORRED]
    XOR RAX, RAX
    MOV AL, BYTE PTR [RBX + 0CH]
    SHL RAX, 02H
    ADD RDX, RAX
        PUSH RBX
        PUSH RCX
    BLTRECT 0, 0, R10, R11, R12, R13
        POP RCX
        POP RBX
NEXTBHB:
    ADD RBX, BOLTOBJSIZE
    DEC RCX
    CMP RCX, 00H
    JNZ BOLTRENDERHBLOOP
ENDM

GRAPHICSINIT MACRO
    LOCAL TILEMAPLOOP1
    LOCAL TILEMAPLOOP2
    LOCAL TITLECARDLOOP
    LOCAL TILEMAPTOPLOOP
    
    LEA RDX, QWORD PTR [COLORBLACK]
    BLTRECT 0, 0, 0, 0, 1280, 1024
    
    LEA RBX, QWORD PTR [TILEMAP0]
    ADD RBX, 05EH
    LEA RDX, QWORD PTR [tilemap]
    XOR R8, R8
    MOV RCX, 014H
TILEMAPLOOP1:
    MOV AX, WORD PTR [RDX]
    MOV WORD PTR [RBX], AX
    INC RBX
    INC RBX
    INC RDX
    INC RDX
    INC R8
    CMP R8, 0FH
    JNZ TILEMAPLOOP1
        XOR R8, R8
        ADD RBX, 022H
    LOOP TILEMAPLOOP1
    
    LEA RBX, QWORD PTR [TILEMAP1]
    MOV RCX, 020H
    MOV RAX, 05EDH
    XOR R8, R8
TILEMAPTOPLOOP:
    MOV WORD PTR [RBX + R8], AX
    INC R8
    INC R8
    LOOP TILEMAPTOPLOOP
    
    MOV RDX, RBX
    ADD RDX, 03CH
    XOR R8, R8
    MOV RCX, 00H
    MOV RAX, 05EDH
TILEMAPLOOP2:
    MOV WORD PTR [RBX + R8], AX
    MOV WORD PTR [RDX + R8], AX
    INC R8
    INC R8
    INC RCX
    TEST RCX, 01H
    JNZ TILEMAPLOOP2
        ADD R8, 03CH
    CMP RCX, 040H
    JNZ TILEMAPLOOP2
    LOADINNEXTSECTIONOFMAP
    
    MOV RCX, 12H
    LEA RDX, QWORD PTR [TITLECARD]
    LEA RBX, QWORD PTR [TILEMAP1]
    ADD RBX, 018H
TITLECARDLOOP:
    MOV AX, WORD PTR [RDX]
    MOV WORD PTR [RBX], AX
    INC RBX
    INC RBX
    INC RDX
    INC RDX
    LOOP TITLECARDLOOP
    
    LEA RBX, QWORD PTR [TILEMAP1]
    ADD RBX, 0204H
    MOV RAX, 064DH
    MOV WORD PTR [RBX], AX
    ADD RBX, 0140H
    MOV RAX, 0643H
    MOV WORD PTR [RBX], AX
    
    LEA RBX, QWORD PTR [SPRITES]
    MOV RAX, 046000C00000001H
    MOV QWORD PTR [RBX], RAX
    
    LEA RBX, QWORD PTR [SPRITES + 08H]
    MOV RAX, 0DC00B0003B0001H
    MOV QWORD PTR [RBX], RAX
    
    LEA RBX, QWORD PTR [SPRITES + 10H]
    MOV RAX, 0E000B0003A0001H
    MOV QWORD PTR [RBX], RAX
    
    LEA RBX, QWORD PTR [SPRITES + 18H]
    MOV RAX, 0E000D0003C0001H
    MOV QWORD PTR [RBX], RAX
    
    LEA RBX, QWORD PTR [SPRITES + 03F8H]
    MOV RAX, 0000000000C0001H
    MOV QWORD PTR [RBX], RAX
    
    MOV AL, 0FFH
    MOV BYTE PTR [SHIPFUEL], AL
ENDM

ENDIF
